# Standalone dev / test / build flake for the goodnet-security-null
# plugin. See `plugins/security/noise/flake.nix` for the canonical
# pattern; this flake is a copy with plugin-specific knobs swapped in.
#
# goodnet-standalone-plugin: null
{
  description = "GoodNet security plugin: null — standalone plugin flake.";

  inputs = {
    goodnet.url     = "path:../../..";
    nixpkgs.follows = "goodnet/nixpkgs";
  };

  outputs = { self, nixpkgs, goodnet }:
    let
      forAllSystems = f:
        nixpkgs.lib.genAttrs [ "x86_64-linux" "aarch64-linux" ]
          (system: f system (import nixpkgs { inherit system; }));
      helpers = goodnet.lib.plugin-helpers;
    in
    {
      packages = forAllSystems (system: pkgs:
        let goodnet-core = goodnet.packages.${system}.goodnet-core;
        in {
          default = pkgs.callPackage ./default.nix { inherit goodnet-core; };
        });

      devShells = forAllSystems (system: pkgs: {
        default = helpers.mkPluginDevShell pkgs {
          plugin = self.packages.${system}.default;
          welcomeText = ''
  goodnet-security-null  —  standalone plugin dev shell
    nix run .#build      — Release build (artefacts → ./build/)
    nix run .#test       — Release build with tests + ctest
    nix run .#test-asan  — ASan + UBSan build + ctest
    nix run .#test-tsan  — TSan build + ctest
    nix run .#debug      — Debug build + gdb on test_null
'';
        };
      });

      apps = forAllSystems (system: pkgs:
        helpers.mkPluginApps pkgs {
          pluginName  = "null";
          debugBinary = "test_null";
        });
    };
}
